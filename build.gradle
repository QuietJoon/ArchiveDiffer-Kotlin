buildscript {
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.2'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.24'
    id 'application'
}

version '0.7.4.0-SNAPSHOT'

repositories {
    mavenCentral()
}

apply plugin: 'com.google.osdetector'

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    def coroutines_version = '1.8.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:$coroutines_version"
    def javafx_version = '21.0.3'
    implementation "org.openjfx:javafx-base:$javafx_version:$platform"
    implementation "org.openjfx:javafx-graphics:$javafx_version:$platform"
    implementation "org.openjfx:javafx-controls:$javafx_version:$platform"
    implementation "org.openjfx:javafx-fxml:$javafx_version:$platform"
    // Version 16.02 does not work with Maven(?) style. Rewrite with file style.
    //implementation group: 'net.sf.sevenzipjbinding', name: 'sevenzipjbinding', version: '16.02-2.01'
    implementation files("JAVALIB_LOCAL/sevenzipjbinding-16.02-2.01-Windows-amd64/lib/sevenzipjbinding.jar")
    implementation files("JAVALIB_LOCAL/sevenzipjbinding-16.02-2.01-Windows-amd64/lib/sevenzipjbinding-Windows-amd64.jar")
    implementation "com.ibm.icu:icu4j:74.2"
    implementation "com.xenomachina:kotlin-argparser:2.0.7"
    implementation "com.googlecode.concurrent-trees:concurrent-trees:2.6.1"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'MainKt'
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}


test {
    useJUnitPlatform()
}
kotlin {
    jvmToolchain(17)
    //jvmToolchain(21)
}
